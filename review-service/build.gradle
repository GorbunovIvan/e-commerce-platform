plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version '0.8.17'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}

ext {
	grpcServerSpringBootStarterVersion = '3.1.0.RELEASE'
	//protocVersion = '4.27.1' // erroneous
	protocVersion = '3.25.3'
	ioGrpcVersion = '1.64.0'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation "net.devh:grpc-server-spring-boot-starter:${grpcServerSpringBootStarterVersion}"
	implementation "com.google.protobuf:protobuf-java:${protocVersion}"
	implementation "io.grpc:grpc-all:${ioGrpcVersion}"
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocVersion}"
	}

	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${ioGrpcVersion}"
		}
	}

	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
